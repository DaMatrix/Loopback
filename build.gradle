/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2019-2019 DaPorkchop_ and contributors
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it. Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

apply plugin: "java"

ext {
    //general things
    javaVersion = "1.8"
    loopbackVersion = "0.0.1-SNAPSHOT"

    //dependency things
    lombokVersion = "1.18.8"
    nettyVersion = "4.1.36.Final"
    porklibVersion = "0.4.0-SNAPSHOT"
}

group "net.daporkchop"
version "$loopbackVersion"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenLocal()
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    mavenCentral()
}

dependencies {
    shade "io.netty:netty-handler:$nettyVersion"
    shade "io.netty:netty-transport-native-epoll:$nettyVersion:linux-x86_64"

    shade "io.netty:netty-transport-sctp:$nettyVersion"

    shade "net.daporkchop.lib:hash:$porklibVersion"
    shade "net.daporkchop.lib:logging:$porklibVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

jar {
    archiveName "loopback-v$loopbackVersion" + ".jar"

    from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }.unique(true)
    } {
        exclude "META-INF/*"
        exclude "LICENSE.txt"
        exclude "dummyThing"
    }

    manifest {
        attributes "Implementation-Title": "DaPorkchop_",
                "Implementation-Version": version,
                "Main-Class": "net.daporkchop.loopback.Loopback"
    }
}
